buildscript {
  repositories {
    mavenCentral()
    maven { url "https://oss.sonatype.org/content/groups/staging" }
    maven { url "https://jitpack.io" }
    maven { url 'https://maven.fabric.io/public' }
  }

  dependencies {
    classpath ("com.glasstowerstudios.gruel:gruel:${project.GRUEL_VERSION}")
    classpath 'com.github.JakeWharton:sdk-manager-plugin:220bf7a'
    classpath 'me.tatarka:gradle-retrolambda:3.2.5'
    classpath 'io.fabric.tools:gradle:1.+'
  }
}


apply plugin: 'android-sdk-manager'
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'me.tatarka.retrolambda'

android {
  compileSdkVersion 23
  buildToolsVersion "23.0.1"

  signingConfigs {
    if (project.hasProperty("GTS_RELEASE_STORE_FILE")) {
      release {
        storeFile file(GTS_RELEASE_STORE_FILE)
        storePassword GTS_RELEASE_STORE_PASSWORD
        keyAlias GTS_RELEASE_KEY_ALIAS
        keyPassword GTS_RELEASE_KEY_PASSWORD
      }
    }
  }

  defaultConfig {
    applicationId "com.glasstowerstudios.rucksack"
    minSdkVersion 15
    targetSdkVersion 23
    versionCode project.RUCKSACK_VERSION_CODE as int
    versionName project.RUCKSACK_VERSION_NAME
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  buildTypes {
    release {
      if (project.hasProperty("GTS_RELEASE_STORE_FILE")) {
        signingConfig signingConfigs.release
      }

      zipAlignEnabled true
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }
}

apply plugin: 'gruel'

gruel {
  archiveName "%appName%", "%version%", "%buildType%", "%commitHash%"
  version android.defaultConfig.versionName
  commitHash gitSha()
}

repositories {
  mavenCentral()
  maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
  maven { url 'https://maven.fabric.io/public' }
}

dependencies {
  compile fileTree(dir: 'libs', include: ['*.jar'])
  testCompile 'junit:junit:4.12'
  compile "com.android.support:appcompat-v7:${SUPPORT_LIBRARY_VERSION}"
  compile "com.android.support:design:${SUPPORT_LIBRARY_VERSION}"
  compile 'com.jakewharton:butterknife:7.0.1'
  compile 'com.google.code.gson:gson:2.6.2'
  compile 'joda-time:joda-time:2.9.1'
  compile "com.android.support:recyclerview-v7:${SUPPORT_LIBRARY_VERSION}"
  compile 'com.hudomju:swipe-to-dismiss-undo:1.0'
  compile "com.android.support:cardview-v7:${SUPPORT_LIBRARY_VERSION}"
  compile 'me.grantland:autofittextview:0.2.1'
  compile 'io.reactivex:rxandroid:1.1.0'
  compile 'au.com.gridstone.rxstore:rxstore:5.0.2'
  compile 'au.com.gridstone.rxstore:converter-gson:5.0.2'

  // Because RxAndroid releases are few and far between, it is recommended you also
  // explicitly depend on RxJava's latest version for bug fixes and new features.
  compile 'io.reactivex:rxjava:1.1.3'

  compile('com.crashlytics.sdk.android:crashlytics:2.5.5@aar') {
    transitive = true;
  }

  testCompile "org.robolectric:robolectric:3.0"

  // For dagger
  apt 'com.google.dagger:dagger-compiler:2.2'
  compile 'com.google.dagger:dagger:2.2'
  provided 'javax.annotation:jsr250-api:1.0'
}
